plugins {
    id "fabric-loom" version "${loom_version}" apply false
}

tasks.register("buildAll") {
    dependsOn subprojects.findAll { it.hasProperty("minecraft_version") }.collect { it.tasks.named("build") }
}

subprojects {
    // Skip intermediate 'versions' container project â€” only configure leaf subprojects
    if (!project.hasProperty("minecraft_version")) return

    apply plugin: "fabric-loom"

    group = rootProject.property("maven_group")
    version = rootProject.property("mod_version")

    base {
        archivesName = "${rootProject.property('archives_base_name')}-${project.property('minecraft_version')}"
    }

    repositories {
        mavenCentral()
        maven { url "https://maven.terraformersmc.com/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.property('minecraft_version')}"
        mappings "net.fabricmc:yarn:${project.property('yarn_mappings')}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.property('loader_version')}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.property('fabric_api_version')}"
        modImplementation "com.terraformersmc:modmenu:${project.property('modmenu_version')}"
    }

    sourceSets {
        main {
            java { srcDir rootProject.file("common/java") }
        }
    }

    processResources {
        inputs.property "version", project.version
        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${project.base.archivesName.get()}" }
        }
    }
}
